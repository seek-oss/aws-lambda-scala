plugins {
    id 'scala'
    id 'com.github.maiflai.scalatest' version '0.31'
}

group 'tekumara'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    def scalaBuildVersion = '2.12'
    def scalaVersion = '2.12.14'

    implementation "org.scala-lang:scala-library:${scalaVersion}"
    implementation "com.typesafe.scala-logging:scala-logging_${scalaBuildVersion}:3.9.4"

    implementation "com.amazonaws:aws-lambda-java-core:1.2.1"
    // provides LambdaAppender which adds AWSRequestId to the MDC
    implementation "com.amazonaws:aws-lambda-java-log4j2:1.3.0"

    implementation "com.lihaoyi:upickle_${scalaBuildVersion}:1.4.2"

    // scala-logging uses slf4j, which we route to log4j
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.15.0"

    testImplementation "org.scalatest:scalatest_${scalaBuildVersion}:3.0.5"
    testImplementation "org.mockito:mockito-all:1.10.19"

    // used by com.github.maiflai.scalatest
    testRuntimeOnly 'org.pegdown:pegdown:1.6.0'
}

// create a reproducible archive, so we only need to upload the zip when its source changes
// https://docs.gradle.org/3.4/userguide/working_with_files.html#sec:reproducible_archives
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// build a zip the way lambda wants it
// see https://docs.aws.amazon.com/lambda/latest/dg/create-deployment-pkg-zip-java.html
task buildZip(type: Zip) {
    // set the base name of the zip file
    baseName = "lambda"
    // no version suffix, gives the built zip a stable name that can be referenced in cloudformation yaml file
    version = ""

    from compileScala
    from processResources
    // the lambda runtime expects to find jars in the lib/ dir
    into('lib') {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip
